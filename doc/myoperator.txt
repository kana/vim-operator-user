*myoperator.txt*	Define your own operator easily

Version 0.0.0
Script ID: 2692
Copyright (C) 2009 kana <http://whileimautomaton.net/>
License: MIT license  {{{
    Permission is hereby granted, free of charge, to any person obtaining
    a copy of this software and associated documentation files (the
    "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish,
    distribute, sublicense, and/or sell copies of the Software, and to
    permit persons to whom the Software is furnished to do so, subject to
    the following conditions:

    The above copyright notice and this permission notice shall be included
    in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
    OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
    CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
    TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
}}}

CONTENTS					*myoperator-contents*

Introduction		|myoperator-introduction|
Interface		|myoperator-interface|
  Commands		  |myoperator-commands|
  Functions		  |myoperator-functions|
Examples		|myoperator-examples|
Bugs			|myoperator-bugs|
Changelog		|myoperator-changelog|




==============================================================================
INTRODUCTION					*myoperator-introduction*

*myoperator* is a Vim plugin to provide an easy way to define your own
|operator|.  Though there are several pitfalls to define operators, you don't
have to take care of them with |:DefineOperator| and/or |myoperator#define()|
which are provided by this plugin.

For example, the following setting defines operator "_" to adjust the height
of the current window to the same as the target range:
>
	DefineOperator _  Op_adjust_window_height
	function! Op_adjust_window_height(motion_wiseness)
	  execute (line("']") - line("'[") + 1) 'wincmd' '_'
	  normal! `[zt
	endfunction
<
You can use this operator as follows:

	10__	Adjust to 10 lines and show the current line at the top.
	_ip	Adjust to the same height of the paragraph under the cursor.

This operator is useful to adjust the visible content in the current window.

See also |myoperator-exmaples| for more examples and explanations.


Requirements:
- Vim 7.2 or later

Latest version:
http://github.com/kana/config/commits/vim-myoperator

Document in HTML format:
http://kana.github.com/config/vim/myoperator.html




==============================================================================
INTERFACE					*myoperator-interface*

------------------------------------------------------------------------------
COMMANDS					*myoperator-commands*

:DefineOperator {lhs}  {function} [{extra}]	*:DefineOperator*
			Define a new operator.
			- {lhs} is a key sequence which is mapped to something
			  to execute the operator.  This key mapping is
			  defined in Normal mode and Visual mode.  {lhs} is
			  also mapped to |g@| in Operator-pending mode to
			  operate linewise like |cc|, |dd|, etc.
			- {function} is the name of a function which is called
			  whenever the operator is executed.
			- {extra} is an arbitrary key sequence which is
			  executed after setting 'operatorfunc' and before
			  starting Operator-pending mode.


------------------------------------------------------------------------------
FUNCTIONS					*myoperator-functions*

myoperator#define({lhs}, {function}, [{extra}])	*myoperator#define()*
			Function version of |:DefineOperator|.

myoperator#load()				*myoperator#load()*
			Load this plugin if it is not loaded yet.  Otherwise,
			does nothing.  Call this function to ensure that
			|myoperator-commands| are defined before you use one
			of them, especially, in your |vimrc|.




==============================================================================
EXAMPLES					*myoperator-examples*

(A) The following setting defines operator-version of |:right|:
>
	DefineOperator L  Op_command_right
	function! Op_command_right(motion_wiseness)
	  '[,']right
	endfunction
<

(B) The following setting defines a generic version of (A).  This is also an
example to use {extra} of |:DefineOperator|:
>
	DefineOperator H  Op_command
	\              call Set_op_command('left')
	DefineOperator L  Op_command
	\              call Set_op_command('right')
	DefineOperator M  Op_command
	\              call Set_op_command('center')

	let s:op_command_command = ''

	function! Set_op_command(command)
	  let s:op_command_command = a:command
	endfunction

	function! Op_command(motion_wiseness)
	  execute "'[,']" s:op_command_command
	endfunction
<




==============================================================================
BUGS						*myoperator-bugs*

- |:DefineOperator| defines a key mapping "{lhs} to |g@|" in
  Operator-pending mode to select text linewise like |cc|, |dd|, etc.
  But this behavior may be harmful in some cases:

  (a) If you define two operators "A" and "B", you can operate the cursor line
  with "AA" and "BB".  But you can ALSO operate the cursor line with "AB" and
  "BA".  These "{operator-A}{operator-B}" key combinations are not expected to
  work as if "{operator-A}{operator-A}", because there is no such default key
  binding.

  (b) If you already define {lhs} to something in Operator-pending mode,
  :DefineOperator always overrides the key mapping.

  To avoid these problems, define "named" key sequences first, then map
  something that you want to type to them.  For example:
>
	DefineOperator <Plug>(operator-A)  OperatorFunctionA
	nmap A  <Plug>(operator-A)
	vmap A  <Plug>(operator-A)
<
  See also the following article for the details.  The article is also useful
  if you want to write your own plulgin:
  ``Vim: Plugins should provide "named" key sequences to customize hotkeys,
  not variables'' <http://whileimautomaton.net/2008/09/27/02/27/35/diary>




==============================================================================
CHANGELOG					*myoperator-changelog*

0.0.0	2009-06-26T02:51:29+09:00
	- Initial version.




==============================================================================
vim:tw=78:ts=8:ft=help:norl:fen:fdl=0:fdm=marker:
